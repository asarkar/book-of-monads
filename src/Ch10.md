```
newtype (f :.: g) a = Compose (f (g a))

newtype Reader r a = Reader
  { runReader :: r -> a
  }

newtype Writer w a = Writer {runWriter :: (a, w)}
```

> Exercise 10.1. The `Writer` monad admits a distributive law with any 
other monad from `Writer w :.: m` to `m :.: Writer w`.
The `Reader` monad admits a dual distributive law from `m :.: Reader r`
to `Reader r :.: m`.
Write the corresponding `swap` functions, and think about which converse 
operations do not exist, in general.

> The complement to both of these functions are not possible to implement
because `(>>=)` works by flattening an `m (m a)` However, we need `(>>=)`
to create a result with `Reader,Writer` as the outer monad (which isn't
possible).

```
swapWriter :: Monad m => (Writer w :.: m) a -> (m :.: Writer w) a
-- c :: Writer w m
swapWriter (Compose c) =
  Compose $
    -- runWriter :: Writer w a -> (a, w)
    let (ma, w) = runWriter c
     in ma >>= \a -> pure $ Writer (a, w)

swapReader :: Monad m => (m :.: Reader r) a -> (Reader r :.: m) a
-- c :: m (Reader r a)
swapReader (Compose c) = Compose . Reader $
  \r ->
    -- rra :: Reader r a
    c >>= \rra ->
      -- runReader :: Reader r a -> r -> a
      pure $ runReader rra r
```

newtype Listed m a = Listed
  { unListed :: [m a]
  }

> Exercise 10.2. Write the `Functor` and `Applicative` instances
for `Listed m`.

```
instance Functor m => Functor (Listed m) where
  {-
  f :: (a -> b)
  xs :: [m a]
  -}
  fmap f (Listed xs) = Listed $ fmap (fmap f) xs

instance Applicative m => Applicative (Listed m) where
  pure a = Listed [pure a]
  {-
  fab :: [m (a -> b)]
  xs :: [m a]
  Listed fab <*> Listed xs = Listed $ [mf <*> x | mf <- fab, x <- xs]
  -}
  Listed fab <*> Listed xs = Listed $ (<*>) <$> fab <*> xs
```
